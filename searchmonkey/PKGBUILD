# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Dmitry N. Shilov aka StormBlast <stormblast@land.ru>

pkgname=searchmonkey
pkgver=2.0.0
pkgrel=2
pkgdesc="A powerful GUI search utility for matching regex patterns"
arch=('i686' 'x86_64')
url="http://searchmonkey.sourceforge.net/"
license=('GPL3')
depends=('qt4')
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}_v$pkgver.zip
        $pkgname-$pkgver-gcc47.diff
        $pkgname.desktop)
sha256sums=('116ebe50c8daff6fa3366ca19087ac1c7d6a40e0c39b9e3c0f4cb6ed66ee87b1'
            '02e0a6fefac84c24850184b097c620cbfa97fc44d29592b9f818a1a056a22425'
            'fa6367d42cf4883aa634e96b11ffe72c44321eb705f5ec50d42fb82511f58dae')

build() {
  cd "$srcdir/${pkgname}_v$pkgver"
  patch -Np1 -i ../$pkgname-$pkgver-gcc47.diff
  qmake-qt4 && make
}

package() {
  cd "$srcdir/${pkgname}_v$pkgver"
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
