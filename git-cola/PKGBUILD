# Maintainer: TDY <tdy@archlinux.info>

pkgname=git-cola
pkgver=1.8.2
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('any')
url="http://git-cola.github.com/"
license=('GPL')
depends=('git' 'python2-pyqt>=4.3')
makedepends=('asciidoc' 'docbook-xsl' 'rsync' 'xmlto' 'python2-sphinx>=1.1.3')
optdepends=('python2-pyinotify>=0.7.1: for inotify support'
            'tk: to use the built-in ssh-askpass handler')
options=('!makeflags')
source=(http://$pkgname.github.com/releases/$pkgname-$pkgver.tar.gz)
sha256sums=('457f27f02c34cff4bc515a966228ae1362d90cb3959e73d6dd5c2e54a56bdd5a')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i 's,= python,&2,' Makefile
	sed -i 's,sphinx-build,&2,' share/doc/git-cola/Makefile
	sed -i 's,env python,&2,' setup.py
	make all doc html
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}

# vim:set ts=2 sw=2 et:
