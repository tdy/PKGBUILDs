# Maintainer: TDY <tdy@archlinux.info>

pkgname=python-webiopi
pkgver=0.5.3
pkgrel=1
pkgdesc="A Raspberry Pi REST framework to control the GPIO and more"
arch=('armv6h')
url="http://code.google.com/p/webiopi/"
license=('APACHE')
depends=('python')
makedepends=('python-distribute')
install=webiopi.install
source=(http://webiopi.googlecode.com/files/WebIOPi-$pkgver.tar.gz
        webiopi.service)
sha256sums=('b21d1b36d273eaf95b6ea9555cf857f7007c0a054410ce75a7418de85b900d40'
            '620c03fb8a190d3b395dd512084f1cfb99538f89cfe05a24fd9180a855529d73')

build() {
  cd "$srcdir/WebIOPi-$pkgver/python"
  python3 setup.py build
}

package() {
  cd "$srcdir/WebIOPi-$pkgver/python"
  python3 setup.py install --root="$pkgdir" --optimize=1

  # passwd
  install -Dm755 ${pkgname#*-}-passwd.py "$pkgdir/usr/bin/${pkgname#*-}-passwd"
  install -Dm644 passwd "$pkgdir/etc/${pkgname#*-}/passwd"

  # resources
  install -dm755 "$pkgdir/usr/share/${pkgname#*-}/htdocs/app/expert"
  install -m644 ../htdocs/app/expert/* \
    "$pkgdir/usr/share/${pkgname#*-}/htdocs/app/expert"
  install -m644 ../htdocs/*.* "$pkgdir/usr/share/${pkgname#*-}/htdocs"

  # service
  install -Dm644 "$srcdir/${pkgname#*-}.service" \
      "$pkgdir/usr/lib/systemd/system/${pkgname#*-}.service"
}

# vim:set ts=2 sw=2 et:
