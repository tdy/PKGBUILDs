# Maintainer: TDY <tdy@archlinux.info>

pkgname=python2-glumpy
pkgver=0.2.1
pkgrel=1
pkgdesc="Rapid visualization of numpy arrays using opengl"
arch=('any')
url="https://code.google.com/p/glumpy/"
license=('BSD')
depends=('ipython2' 'python2-opengl' 'python2-numpy')
optdepends=('python2-matplotlib: required by some of the bundled demos'
            'python2-scipy: required by some of the bundled demos')
options=(!emptydirs)
source=(https://glumpy.googlecode.com/files/${pkgname#*-}-$pkgver.tar.gz)
sha256sums=('9e43ff90bcba2ad1c3dfd3e009f926e307a79e423e82e887e2b2d040924126df')

package() {
  cd "$srcdir/${pkgname#*-}-$pkgver"
  find -name '*.py' -exec sed -i '1s/python/&2/' '{}' \;
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # demos
  install -dm755 "$pkgdir/usr/share/$pkgname/"
  install -m755 demos/*.py "$pkgdir/usr/share/$pkgname/"
  install -m644 demos/*.{obj,png} "$pkgdir/usr/share/$pkgname/"
}

# vim:set ts=2 sw=2 et:
