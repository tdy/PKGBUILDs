# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>

pkgname=pyvnc2swf
pkgver=0.9.5
pkgrel=2
pkgdesc="A screen recording tool for swf and flv formats"
arch=('i686' 'x86_64')
url="http://www.unixuser.org/~euske/vnc2swf/pyvnc2swf.html"
license=('GPL')
depends=('python2-pygame>=1.6' 'x11vnc')
optdepends=('python2-pymedia>=1.3.5: MPEG encoding')
source=(http://www.unixuser.org/~euske/vnc2swf/$pkgname-$pkgver.tar.gz
        $pkgname-$pkgver-setup.diff)
sha256sums=('0c097dd5c26c47cd61eb0c40f02fe8345e920e89b22163e7d0c70d1432f33c99'
            '21775fae4ec2d1648e7099e94ac24ce7fd058890d27cbe07ea878525ce72079e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../$pkgname-$pkgver-setup.diff
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
