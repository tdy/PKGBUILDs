# Maintainer: TDY <tdy@archlinux.info>

pkgname=yzis
pkgver=1.0_alpha1
pkgrel=1
pkgdesc="A fast, powerful editor inspired by Vim"
arch=('i686' 'x86_64')
url="http://freecode.com/projects/yzis"
license=('LGPL' 'GPL')
depends=('file' 'lua51' 'qt')
makedepends=('cmake' 'doxygen')
optdepends=('qyzis: QT interface')
source=(http://dl.freehackers.org/$pkgname/$pkgname-${pkgver/_/-}.tar.bz2
        $pkgname-$pkgver-logging.diff)
md5sums=('9627cb52f214754225ee632695933ad5'
         '660a16d96f680a61396d5b42b276ff6a')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  patch -Np1 -i ../$pkgname-$pkgver-logging.diff

  cd cmake
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_QYZIS=OFF \
    -DENABLE_LIBYZISRUNNER=OFF \
    -DENABLE_DOCUMENTATION=OFF \
    -DLIBLUA51_INCLUDE_DIR=/usr/include/lua5.1
  make
}

check() {
  cd "$srcdir/$pkgname-${pkgver/_/-}/cmake"
  make test
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}/cmake"
  make DESTDIR="$pkgdir/" install
  ln -sf n$pkgname "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
