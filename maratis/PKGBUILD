# Maintainer: TDY <tdy@archlinux.info>

pkgname=maratis
pkgver=3.1d_beta
pkgrel=1
pkgdesc="A portable, simple, and visual game development tool for artists and developers"
arch=('i686' 'x86_64')
url="http://www.maratis3d.org/"
license=('GPL')
depends=('libgl' 'libsndfile' 'libxxf86vm' 'openal')
[[ $CARCH == x86_64 ]] && depends=(${depends[@]/#/lib32-})
[[ $CARCH == i686 ]] && makedepends=('chrpath')
source=(http://maratis.googlecode.com/files/Maratis-${pkgver/_/-}-linux-x86.zip
        MaratisEditor.sh
        MaratisEditor.desktop
        MaratisPlayer.sh
        MaratisPlayer.desktop)
sha256sums=('72648734ecf3e68b80447e0823167aa1070a0912de03c50d9bf7a5efd67e2d48'
            'e4d6d83d1e1808c87b39c0dfdae05a3ccc7269449ca4b9bc67bee2cc3dc3c882'
            'cd9246a11466e36f42ae930026be1c408dd609423f76496d315718fde676f350'
            '310d6145578b2f2edf291d37e7d18fee024cf95ed16f164d62a31d783cbacdcf'
            '7b987e3e794dc76023ea6b68f8f5e76bd48c07ace5ad5f6b53d07fa58779a893')

build() {
  cd "$srcdir/Maratis-${pkgver/_/-}-linux-x86/Bin"

  # delete rpaths (unfortunately only for i686; chrpath can't do cross-arch formatting)
  if [[ $CARCH == i686 ]]; then
    chrpath -d libM{Engine,Core}.so Maratis{Editor,Player}
  fi
}

package() {
  cd "$srcdir/Maratis-${pkgver/_/-}-linux-x86/Bin"

  # libs
  install -Dm755 libMEngine.so "$pkgdir/usr/lib/libMEngine.so"
  install -Dm755 libMCore.so "$pkgdir/usr/lib/libMCore.so"

  # gui
  install -dm755 "$pkgdir/opt/$pkgname/gui/meshs"
  install -m644 gui/*.* "$pkgdir/opt/$pkgname/gui"
  install -m644 gui/meshs/*.* "$pkgdir/opt/$pkgname/gui/meshs"
  install -Dm644 font/default.tga "$pkgdir/opt/$pkgname/font/default.tga"

  # bins
  install -Dm755 MaratisEditor "$pkgdir/opt/$pkgname/MaratisEditor"
  install -Dm755 MaratisPlayer "$pkgdir/opt/$pkgname/MaratisPlayer"
  install -Dm755 "$srcdir/MaratisEditor.sh" "$pkgdir/usr/bin/MaratisEditor"
  install -Dm755 "$srcdir/MaratisPlayer.sh" "$pkgdir/usr/bin/MaratisPlayer"

  # desktop entries
  install -Dm644 "$srcdir/MaratisEditor.desktop" \
    "$pkgdir/usr/share/applications/MaratisEditor.desktop"
  install -Dm644 "$srcdir/MaratisPlayer.desktop" \
    "$pkgdir/usr/share/applications/MaratisPlayer.desktop"
  install -Dm644 gui/Title.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
