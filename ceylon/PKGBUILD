# Maintainer: TDY <tdy@archlinux.info>

pkgname=ceylon
pkgver=0.4
pkgrel=1
pkgdesc="A powerful statically typed language for writing large programs in groups"
arch=('any')
url="http://ceylon-lang.org/"
license=('Apache' 'GPL' 'LGPL')
depends=('java-environment>=7')
optdepends=('nodejs: for JavaScript interoperability')
install=$pkgname.install
source=(http://cloud.github.com/downloads/$pkgname/$pkgname-dist/$pkgname-$pkgver.zip)
sha256sums=('4ea19aab875d426af95f707dbfad944ada303f288846507963b8da30bf4284e5')

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm644 bin/$pkgname-completion.bash "$pkgdir/usr/share/java/$pkgname/$pkgname-completion.bash"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  # libs
  cp -a doc lib {,runtime-}repo samples templates "$pkgdir/usr/share/java/$pkgname/"
  find "$pkgdir/usr/share/java/$pkgname/" -type f -exec chmod 644 '{}' \;
  find "$pkgdir/usr/share/java/$pkgname/" -type d -exec chmod 755 '{}' \;

  # bins
  install -Dm755 bin/$pkgname "$pkgdir/usr/share/java/$pkgname/bin/$pkgname"
  install -Dm644 bin/$pkgname-cp.sh "$pkgdir/usr/share/java/$pkgname/bin/$pkgname-cp.sh"
  install -dm755 "$pkgdir/usr/bin/"
  ln -s /usr/share/java/$pkgname/bin/$pkgname "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
