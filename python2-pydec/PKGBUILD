# Maintainer: TDY <tdy@archlinux.info>

pkgname=python2-pydec
pkgver=29
pkgrel=1
pkgdesc="A Python library for the discretization of exterior calculus"
arch=(any)
url="http://code.google.com/p/pydec/"
license=(BSD)
depends=(python2-numpy python2-scipy python2-matplotlib)
makedepends=(subversion)
options=(!emptydirs)

_svntrunk=http://pydec.googlecode.com/svn/trunk/
_svnmod=pydec

build() {
  cd "$srcdir"

  if [[ -d $_svnmod/.svn ]]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  rm -rf $_svnmod-build
  svn export $_svnmod $_svnmod-build
  cd $_svnmod-build

  find -name '*.py' -exec sed -i '1s/python/&2/' '{}' \;
  python2 setup.py build
}

package() {
  cd "$srcdir/$_svnmod-build"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
