# Maintainer:  TDY <tdy@archlinux.info>

pkgname=echinus-git
pkgver=20121228
pkgrel=1
pkgdesc="A window manager for X in the spirit of dwm"
arch=('i686' 'x86_64')
url="http://plhk.ru/echinus"
license=('MIT')
depends=('libxft' 'libxrandr')
makedepends=('git' 'pkgconfig')
optdepends=('ourico: complementary taskbar for echinus')
provides=('echinus')
conflicts=('echinus')
install=echinus.install
source=(echinus.desktop)
sha256sums=('8e86f3550b66701fe2f481b556df05b0d06647b470f1adb8bfa50cd55df4bf9b')

_gitroot=git://github.com/polachok/echinus.git
_gitname=echinus

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make PREFIX=/usr X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir" CONF=/share/$_gitname/examples install
  install -Dm644 $_gitname.1 "$pkgdir/usr/share/man/man1/$_gitname.1"
  install -Dm644 README "$pkgdir/usr/share/doc/$_gitname/README"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 ../$_gitname.desktop \
    "$pkgdir/usr/share/xsessions/$_gitname.desktop"
}

# vim:set ts=2 sw=2 et:
