# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=arkanoidsb
pkgver=1.3.2.10
pkgrel=1
pkgdesc="Arkanoid: Space Ball"
url="http://www.wegroup.org/games/arkanoid-games/arkanoid-space-ball.html"
arch=('i686' 'x86_64')
license=('custom')
depends=('sdl_mixer' 'sdl_image' 'libogg')
[[ $CARCH == x86_64 ]] && depends=(${depends[@]/#/lib32-})
source=(http://www.wegroup.org/downloads/$pkgname-$pkgver.tar.bz2
        $pkgname.desktop
        $pkgname.sh)
sha256sums=('4eedf4205f11290e0e168c0224829056f5b5822fa0c99e06de1ed94bd4705669'
            '4f6af005c50d38afb46724f0810d16806ecf98e5513d25ebcdd08c97d6e7de2d'
            'f6b0c56c8389a2c977737453cbbcd9955c4ae2e0009f582662b6278d76e2d4f1')

package() {
  cd "$srcdir/$pkgname"
  install -Dm755 ../$pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm755 $pkgname "$pkgdir/usr/lib/$pkgname/$pkgname"

  # res
  install -Dm644 res/$pkgname.pak "$pkgdir/usr/lib/$pkgname/res/$pkgname.pak"
  install -Dm644 res/module01.ogg "$pkgdir/usr/lib/$pkgname/res/module01.ogg"
  install -Dm644 res/module02.ogg "$pkgdir/usr/lib/$pkgname/res/module02.ogg"
  install -Dm644 res/module03.s3m "$pkgdir/usr/lib/$pkgname/res/module03.s3m"

  # desktop entry
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # license
  install -Dm644 README "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
