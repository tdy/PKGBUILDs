# Maintainer: TDY <tdy@archlinux.info>

pkgname=whatsup
pkgver=1.14
pkgrel=1
pkgdesc="A cluster node up/down detection utility"
arch=('i686' 'x86_64')
url="https://computing.llnl.gov/linux/whatsup.html"
license=('GPL')
depends=('perl')
makedepends=('chrpath')
provides=('pingd')
optdepends=('ganglia: a backend alternative to the bundled pingd'
            'cerebro: a backend alternative to the bundled pingd')
options=('!libtool' '!emptydirs')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        pingd.rc
        pingd.service)
sha256sums=('1b476e2e9f5c531b8dc1475ef45ce883be03c60aa2538535cabf7b736f9fe555'
            '639e8e68a0ab5dfd088d9c9ae786b539d5adc71e6124b60a2a764ea98761ebf4'
            'a6d8366f2a37af3539cbeb447c38cce64f78460f0e35b1e749eafc29e7cf5d24')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/init\.d//' etc/Makefile.in
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -Dm755 ../pingd.rc "$pkgdir/etc/rc.d/pingd"
  install -Dm644 ../pingd.service "$pkgdir/usr/lib/systemd/system/pingd.service"
  chrpath -d "$pkgdir/usr/lib/perl5/vendor_perl/auto/Libnodeupdown/Libnodeupdown.so"
}

# vim:set ts=2 sw=2 et:
