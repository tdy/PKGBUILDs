# Maintainer: TDY <tdy@archlinux.info>

pkgname=skype4py-git
pkgver=20130107
pkgrel=1
pkgdesc="Python wrapper for the Skype API"
arch=('any')
url="https://github.com/awahlig/skype4py"
license=('BSD')
depends=('python2-dbus' 'python2-gobject2' 'skype')
makedepends=('git' 'python2-distribute')
provides=('skype4py' 'skype4py-svn')
conflicts=('skype4py' 'skype4py-svn')

_gitroot=https://github.com/awahlig/skype4py.git
_gitname=skype4py

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname-build"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 Skype4Py/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
