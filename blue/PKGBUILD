# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=blue
pkgver=1.7.5
pkgrel=2
pkgdesc="A unique, dynamic programming language with a minimalistic, intuitive syntax"
arch=('i686' 'x86_64')
url="http://www.lechak.info/blue/"
license=('GPL')
depends=('freetype2' 'libpng' 'mesa')
source=(http://www.lechak.info/$pkgname/$pkgname$pkgver.tgz
        $pkgname-$pkgver-png150.diff)
sha256sums=('888c5798a1b482cd655049e05d552e59e0cb19c941b4f3979d28d783f6f675b3'
            '57c92ddaaa02e4fb497239d9033f32bc5c8523389b10c75063cd937dcf80cf33')

build() {
  cd "$srcdir/$pkgname"
  patch -Np1 -i ../$pkgname-$pkgver-png150.diff
  sed -i 's/CFLAGS= -Wall -O3/CFLAGS+=/; /ln/d' Makefile
  make
}

package() {
  cd "$srcdir/$pkgname"
  make prefix="$pkgdir/usr/lib/" install
  install -dm755 "$pkgdir/usr/bin/"
  ln -sf ../lib/$pkgname/$pkgname "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
