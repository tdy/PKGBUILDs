# Maintainer: TDY <tdy@archlinux.info>

pkgname=geh
pkgver=20100526
pkgrel=1
pkgdesc="A simple image viewer and background setter"
arch=('i686' 'x86_64')
url="http://pekwm.org/projects/5/"
license=('MIT')
depends=('gtk2>=2.8.0')
makedepends=('pkgconfig>=0.16')
source=($pkgname-$pkgver.tar.bz2::http://pekwm.org/projects/5/repository/snapshot?revision=490b17fd270707447419333ceb7bde85eca01a99)
sha256sums=('6dec039e4137f976bba5b441ce2ccd84e7147fb7e7fd0413e121c69fe7fb9607')

build() {
  cd "$srcdir/$pkgname"
  sed -n '2,22 s/ \*[ ]*//p' src/main.c > license.txt
  ./autogen.sh && ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm755 src/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

# vim:set ts=2 sw=2 et:
