# Maintainer: TDY <tdy@archlinux.info>

pkgname=oleo-motif
pkgver=1.99.16
pkgrel=3
pkgdesc="GNU's lightweight spreadsheet program with a Motif-based interface"
arch=('i686' 'x86_64')
url=http://www.gnu.org/software/oleo/
license=('GPL2')
depends=('libcups' 'xbae')
makedepends=('texi2html')
provides=('oleo')
conflicts=('oleo')
install=oleo.install
source=(http://ftp.gnu.org/pub/gnu/${pkgname/-*}/${pkgname/-*}-$pkgver.tar.gz
        https://answers.launchpad.net/debian/+archive/primary/+files/${pkgname/-*}_$pkgver-11.diff.gz)
sha256sums=('6598df85d06ff2534ec08ed0657508f17dbbc58dd02d419160989de7c487bc86'
            'c3f54bc193a4b60255196f6f1e4edc6659e85587b857f0f3519fb5db390525ec')

build() {
  cd "$srcdir/${pkgname/-*}-$pkgver"
  patch -Np1 -i ../${pkgname/-*}_$pkgver-11.diff
  sed -i '/MAKE/s/install-data-am//' Makefile.in
  ./configure --prefix=/usr --infodir=/usr/share/info
  find -type f -exec sed -i \
    -e 's|the file COPYING for the|the license for the conditions:|g' \
    -e 's|conditions\.|/usr/share/licenses/common/GPL2/license.txt|g' \
    -e '/#define NeedFunctionPrototypes /s/0/1/' '{}' \;
  make
}

package() {
  cd "$srcdir/${pkgname/-*}-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 doc/${pkgname/-*}.html \
    "$pkgdir/usr/share/doc/$pkgname/${pkgname/-*}.html"
}

# vim:set ts=2 sw=2 et:
