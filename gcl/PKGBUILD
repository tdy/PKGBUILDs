# Maintainer: TDY <tdy@archlinux.info>

pkgname=gcl
pkgver=2.7.0_101
pkgrel=1
pkgdesc="GNU Common Lisp interpreter and compiler"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gcl/"
license=('GPL' 'LGPL')
depends=('gmp' 'tk')
options=('!makeflags')
install=gcl.install
source=(http://ftp.debian.org/debian/pool/main/g/gclcvs/gclcvs_${pkgver/_/-}.tar.gz)
sha256sums=('126fc7cd5320473425e4998d14c281c7294c96a38f56b3f698cdc2d267c01b70')

build() {
  cd "$srcdir/gclcvs-${pkgver%_*}"
  ./configure --prefix=/usr \
      --disable-xgcl \
      --without-x \
      --disable-dynsysgmp \
      --enable-readline
  make
}

package() {
  cd "$srcdir/gclcvs-${pkgver%_*}"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
