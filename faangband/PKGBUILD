# Maintainer: TDY <tdy@archlinux.info>

pkgname=faangband
pkgver=1.3.1
pkgrel=2
pkgdesc="An Angband variant set in the First Age of Tolkien's Silmarillion world"
arch=('i686' 'x86_64')
url="http://angband.oook.cz/faangband/"
license=('GPL' 'custom')
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf')
source=(http://angband.oook.cz/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('98bd6dcfe8ae5cd2bdbeec6bca060d4932e57106d51f4b0f3f1551c577e36a56')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --with-configpath=/etc/$pkgname \
    --enable-sdl
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 copying.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  find "$pkgdir/usr/share/$pkgname" -name delete.me -delete
}

# vim:set ts=2 sw=2 et:
