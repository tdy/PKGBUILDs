# Maintainer: TDY <tdy@archlinux.info>

pkgname=cchess
pkgver=1.1.0
pkgrel=1
pkgdesc="A simple network client for chinese chess"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/cchess/"
license=('GPL')
depends=('sdl_mixer')
[[ $CARCH == x86_64 ]] && depends=('lib32-sdl_mixer')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname.$pkgver.linux.tar.gz
        $pkgname.launcher
        $pkgname.menu)
sha256sums=('7e5ce1ff3bf940ed28897bd023f503acf17c9fc18b4163d6c4c05785860a42d8'
            'ab0e2b89e7ec42389bf9bd6020700f35379866184db489421c415d86da819f4b'
            '83f18ffbee4a9da07e5c1c58d01db3655fbc3a7ddb86ee379ebb400c9c4bc3c0')

build() {
  cd "$srcdir/$pkgname.$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname.$pkgver"
  install -dm755 "$pkgdir/usr/share/$pkgname/res"
  find res -type f -exec install -Dm644 '{}' "$pkgdir/usr/share/$pkgname/res" \;
  install -Dm644 res/r_j.bmp "$pkgdir/usr/share/pixmaps/$pkgname.bmp"
  install -Dm755 $pkgname "$pkgdir/usr/share/$pkgname/$pkgname"
  install -Dm755 ../$pkgname.launcher "$pkgdir/usr/bin/$pkgname"
  install -Dm644 ../$pkgname.menu "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
