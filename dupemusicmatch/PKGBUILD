# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Leigh <leighallison@earthlink.net.com>
# Contributor: FJ <joostef@gmail.com>

pkgname=dupemusicmatch
pkgver=2.3
pkgrel=3
pkgdesc="A tool to quickly find duplicate MP3/Ogg/FLAC files via letter matching"
arch=('any')
url="http://dupemusicmatch.sourceforge.net/"
license=('GPL')
depends=('mutagen' 'python2-numpy')
optdepends=('pygtk>=2.8.0: for GUI support'
            'audacious:    for GUI support')
source=(http://downloads.sourceforge.net/$pkgname/DupeMusicMatch-$pkgver.tar.gz
        dmm.desktop dmm.launcher dmm.png)
sha256sums=('794e0950f9fae15949607639f79948bd4d922afaa1df911ef134ef87d94f7589'
            'ede9e0f677207c671eccc677606efdd89c5e0035d14b4087d924b552b664cb8c'
            'c8bbeba5416c1d336088cef2e5a89a5f9f667d84febe4260006ba11867a661b5'
            '59bbd5ee6083fbb5e3613ae1029ccda3970f79b260755cb4c35f63c02517e327')

build() {
  cd "$srcdir/DupeMusicMatch-$pkgver"

  # port to python2-numpy
  python2 -c 'import numpy.oldnumeric.alter_code1 as noa; noa.converttree(".")'
  sed -i '1s/python/&2/' *.py
  sed -i '/^import/s/Numeric, //;/^import/a\import numpy.oldnumeric as Numeric' core.py
}

package() {
  cd "$srcdir/DupeMusicMatch-$pkgver"

  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -cm644 *.py *.glade "$pkgdir/usr/share/$pkgname"
  install -Dm644 locale/de/LC_MESSAGES/$pkgname.mo \
    "$pkgdir/usr/share/locale/de/LC_MESSAGES/$pkgname.mo"

  install -Dm755 ../dmm.launcher "$pkgdir/usr/bin/dmm"
  install -Dm644 ../dmm.desktop "$pkgdir/usr/share/applications/dmm.desktop"
  install -Dm644 ../dmm.png "$pkgdir/usr/share/pixmaps/dmm.png"
}

# vim:set ts=2 sw=2 et:
