# Maintainer: TDY <tdy@archlinux.info>

pkgname=cerebro
pkgver=1.16
pkgrel=1
pkgdesc="A collection of cluster monitoring tools and libraries"
arch=('i686' 'x86_64')
url="https://computing.llnl.gov/linux/cerebro.html"
license=('GPL')
options=('!libtool')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        cerebrod.rc
        cerebrod.service)
sha256sums=('31ebba3e3e50cb052b06aa913e38030200f810dd0abd96848ff38d85d88e3b78'
            '1e5a6d6c259798c4443c85481504f6b66adb4938cf87b0217790116a73207991'
            '81b0a7936e4fad3e0f133d46772979b009ae8e7794f438ded6e1dee2f16ca7cb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/init\.d/rc.d/g' etc/Makefile.in
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -Dm755 ../cerebrod.rc "$pkgdir/etc/rc.d/cerebrod"
  install -Dm644 ../cerebrod.service "$pkgdir/usr/lib/systemd/system/cerebrod.service"
}

# vim:set ts=2 sw=2 et:
