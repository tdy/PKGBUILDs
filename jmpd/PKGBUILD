# Maintainer: TDY <tdy@archlinux.info>

pkgname=jmpd
pkgver=0.1.2
pkgrel=1
pkgdesc="A simple Java client for MPD"
arch=('any')
url="http://en.tom000.info/portfolio/jmpd_-_mpd_client.html"
license=('GPL3')
depends=('java-environment')
source=(http://tom000.info/share/jmpd/jmpd-$pkgver.tar.bz2
        jmpd.desktop)
sha256sums=('d52bb9980931a8a8851821510dfc7b5b778fc423ed02f23fdbefd8ca81e5296d'
            '71c46eae84e9d796e19b1bd828d4d93b625a46bf7005d03f3dee8c487fb98d78')

package() {
  cd "$srcdir/jMPD"
  sed -i 's+opt/jMPD+usr/share/java/jmpd+; s+$+ &> /tmp/jmpd.log+' jMPD
  install -Dm755 jMPD "$pkgdir/usr/share/jmpd/jMPD"
  install -Dm644 jMPD.jar "$pkgdir/usr/share/java/jmpd/jMPD.jar"
  install -Dm644 lib/AbsoluteLayout.jar \
    "$pkgdir/usr/share/java/jmpd/lib/AbsoluteLayout.jar"
  install -Dm644 jMPD.png "$pkgdir/usr/share/pixmaps/jmpd.png"
  install -Dm644 ../jmpd.desktop "$pkgdir/usr/share/applications/jmpd.desktop"
  install -dm755 "$pkgdir/usr/bin"
  ln -sf /usr/share/jmpd/jMPD "$pkgdir/usr/bin/jmpd"
}

# vim:set ts=2 sw=2 et:
