# Maintainer: TDY <tdy@archlinux.info>

pkgname=divfixpp-svn
pkgver=96
pkgrel=1
pkgdesc="A tool for repairing corrupted AVI file streams"
arch=('i686' 'x86_64')
url="http://divfixpp.sourceforge.net/"
license=('GPL')
depends=('wxgtk')
makedepends=('gettext' 'subversion')
provides=('divfixpp')
conflicts=('divfixpp')

_svntrunk=https://divfixpp.svn.sourceforge.net/svnroot/divfixpp
_svnmod=divfixpp

build() {
  cd "$srcdir"

  msg "Connecting to divfixpp.svn.sf.net..."
  if [[ -d $_svnmod/.svn ]]; then
    (cd $_svnmod && svn up -r $pkgver)
    msg2 "Local files updated"
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    msg2 "SVN checkout done"
  fi

  rm -rf $_svnmod/trunk-build
  cp -r $_svnmod/trunk $_svnmod/trunk-build
  cd $_svnmod/trunk-build

  msg "Starting make..."
  if [[ ! $(grep -q '#include\s*<stdint\.h>' src/DivFix++Core.h) ]]; then
    sed -i '42 a\#include <stdint.h>' src/DivFix++Core.h
  fi
  make
}

package() {
  cd "$srcdir/$_svnmod/trunk-build"
  make DESTDIR="$pkgdir/" install
  ln -sf DivFix++ "$pkgdir/usr/bin/$_svnmod"
}

# vim:set ts=2 sw=2 et:
