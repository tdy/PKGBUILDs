# Maintainer: TDY <tdy@archlinux.info>

pkgname=echinus
pkgver=0.4.9
pkgrel=1
pkgdesc="A window manager for X in the spirit of dwm"
arch=('i686' 'x86_64')
url="http://plhk.ru/echinus"
license=('MIT')
depends=('libxft' 'libxrandr')
optdepends=('ourico: complementary taskbar for echinus')
install=echinus.install
source=(http://plhk.ru/static/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('c57ac8f72845bea02bed0c27256b4023668ae7b41ae3c461d6e9b63e24914d94')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s/\${DESTDIR}\(\${CONF}\)/\1/" Makefile
  make PREFIX=/usr \
       CONFPREFIX=/etc/xdg \
       CONF=/etc/xdg/$pkgname
}

package () {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr \
       CONFPREFIX=/etc/xdg \
       CONF="/etc/xdg/$pkgname" \
       DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  find "$pkgdir/etc/xdg/$pkgname" -type f -exec chmod 644 '{}' \;
}

# vim:set ts=2 sw=2 et:
