# Maintainer: TDY <tdy@archlinux.info>

pkgname=cb2bib
pkgver=1.4.9
pkgrel=2
pkgdesc="A tool for parsing clipboard data into BibTeX bibliographic database files"
arch=('i686' 'x86_64')
url="http://www.molspaces.com/cb2bib/"
license=('GPL3')
depends=('desktop-file-utils' 'lzo2' 'qt4')
optdepends=('openssl: network reference query support'
            'perl-exiftool: meta data support'
            'texlive-latex3: file correctness checking; bib2pdf printing'
            'xpdf: pdftotext support')
install=cb2bib.install
source=(http://www.molspaces.com/dl/progs/$pkgname-$pkgver.tar.gz)
sha256sums=('a12e3f05df5f05e180bc1ff3af8cfcb2fc6142cd2ab7d452706e86a449e1b359')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix /usr --qmakepath /usr/bin/qmake-qt4
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
