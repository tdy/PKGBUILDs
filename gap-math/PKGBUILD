# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=gap-math
pkgver=4.6.3
pkgrel=1
pkgdesc="A system for computational discrete algebra"
arch=('i686' 'x86_64')
url="http://www.gap-system.org/"
license=('GPL')
makedepends=('chrpath' 'rsync')
options=('!libtool' '!emptydirs' '!makeflags')
source=(ftp://ftp.gap-system.org/pub/gap/gap4/tar.bz2/gap4r6p2_2013_02_02-01_00.tar.bz2
        gap.sh)
sha256sums=('ab0f3e933df28ec2d4b4bd4a515e3553fa6926aef1c54efed18487ad2e0af8d3'
            'd90a6ac0610b0eea1657297e6d18a3bfcbdee8ca5c180079b55ae7c1af57cca2')

build() {
  cd "$srcdir/gap4r6"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"
  install -dm755 "$pkgdir/opt/gap/"
  rsync -prv --exclude='*.exe' --exclude='*.dll' gap4r6/* "$pkgdir/opt/gap/"
  chrpath -d $(find "$pkgdir" ! -name '*.o' -exec file '{}' \;|grep ELF|cut -d: -f1)
  install -Dm755 gap.sh "$pkgdir/usr/bin/gap"
}

# vim:set ts=2 sw=2 et:
